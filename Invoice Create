REPORT zuiwc_invoice.

SELECTION-SCREEN BEGIN OF BLOCK blk_sel WITH FRAME TITLE TEXT-t01.

  PARAMETERS:
    p_invid TYPE zuiwc_invoice-inv_id OBLIGATORY,
    p_date  TYPE zuiwc_invoice-inv_date OBLIGATORY.

  SELECT-OPTIONS:
    so_fund  FOR zuiwc_gl_config-fund OBLIGATORY,
    so_agcy  FOR zuiwc_invoice-agcy,
    so_prctr FOR zuiwc_invoice-prctr.

  PARAMETERS:
    p_prntr TYPE pri_params-pdest,
    p_form  TYPE fpname,
    p_test  TYPE abap_bool AS CHECKBOX DEFAULT abap_true.

SELECTION-SCREEN END OF BLOCK blk_sel.

SELECTION-SCREEN BEGIN OF BLOCK blk_act WITH FRAME TITLE TEXT-t02.

  PARAMETERS:
    p_post   TYPE abap_bool AS CHECKBOX DEFAULT abap_false,
    p_out    TYPE abap_bool AS CHECKBOX DEFAULT abap_false.

  SELECTION-SCREEN BEGIN OF LINE.
    PARAMETERS:
      p_print RADIOBUTTON GROUP grp_out MODIF ID OUT DEFAULT 'X',
      p_email RADIOBUTTON GROUP grp_out MODIF ID OUT.
  SELECTION-SCREEN END OF LINE.

  PARAMETERS:
    p_pform TYPE abap_bool AS CHECKBOX MODIF ID OUT,
    p_prep  TYPE abap_bool AS CHECKBOX MODIF ID OUT,
    p_onlyp RADIOBUTTON GROUP grp_eml MODIF ID EML DEFAULT 'X',
    p_alla  RADIOBUTTON GROUP grp_eml MODIF ID EML,
    p_att   TYPE abap_bool AS CHECKBOX MODIF ID EML.

SELECTION-SCREEN END OF BLOCK blk_act.

AT SELECTION-SCREEN OUTPUT.
  LOOP AT SCREEN ASSIGNING FIELD-SYMBOL(<ls_screen>).
    IF <ls_screen>-group1 = 'OUT'.
      <ls_screen>-active = COND i( WHEN p_out = abap_true THEN 1 ELSE 0 ).
      MODIFY SCREEN FROM <ls_screen>.
    ELSEIF <ls_screen>-group1 = 'EML'.
      <ls_screen>-active = COND i( WHEN p_out = abap_true AND p_email = abap_true THEN 1 ELSE 0 ).
      MODIFY SCREEN FROM <ls_screen>.
    ENDIF.
  ENDLOOP.

START-OF-SELECTION.

  DATA(ls_params) = VALUE zcl_uiwc_invoice_runner=>ty_params(
    inv_id     = p_invid
    inv_date   = p_date
    test_run   = p_test
    do_post    = p_post
    do_output  = p_out
    out_print  = xsdbool( p_print = abap_true )
    out_email  = xsdbool( p_email = abap_true )
    print_form = p_pform
    print_rep  = p_prep
    email_prim = xsdbool( p_onlyp = abap_true )
    email_all  = xsdbool( p_alla  = abap_true )
    att_rep    = p_att
    printer    = p_prntr
    form_name  = p_form
    so_fund    = so_fund[]
    so_agcy    = so_agcy[]
    so_prctr   = so_prctr[] ).

  DATA(lo_runner) = NEW zcl_uiwc_invoice_runner( is_params = ls_params ).
  lo_runner->execute( ).
